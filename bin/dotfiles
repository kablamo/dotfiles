#!/usr/bin/env perl

use strict;
use warnings;

use Path::Class qw/file dir/;
use File::Copy qw/move cp/;

my $home_dir     = dir($ENV{HOME});
my $home_bin_dir = $home_dir->subdir('bin');

my $dotfiles_dir       = $home_dir->subdir('.dotfiles');
my $dotfiles_bin_dir   = $dotfiles_dir->subdir('bin');
my $dotfiles_links_dir = $dotfiles_dir->subdir('links');
my $dotfiles_copy_dir  = $dotfiles_dir->subdir('copy');

my $backup_dir = dir('/tmp/dotfiles-backup');

create_links( $dotfiles_links_dir => $home_dir     );
create_links( $dotfiles_bin_dir   => $home_bin_dir );
copy_files(   $dotfiles_copy_dir  => $home_dir     );

# update all the git submodules in the top level of $dotfiles_dir
process_submodules($dotfiles_dir => $home_dir);

# done

sub process_submodules {
    my ($dotfiles_dir, $home_dir) = @_;
    my $home_bin_dir = $home_dir->subdir('bin');

    `cd $dotfiles_dir && git submodule init && git submodule update`;

    while (my $dir = $dotfiles_dir->next) {
        next unless -d $dir;
        next if $dir->basename =~ $dotfiles_dir->basename;
        next if $dir->basename =~ /^\./;
        next if $dir =~ /copy$/;
        next if $dir =~ /links$/;
        next if $dir =~ /bin$/;

        my $bin_dir = $dir->subdir('bin');
        -e $bin_dir
            ? create_links($bin_dir => $home_bin_dir, 1)
            : create_links($dir => $home_bin_dir, 1);
    }

}

sub remove_dead_links {
    my $home_dir = shift;

    while (my $file = $home_dir->next) {
        next if -e $file;
        print "unlink $file\n" if -l $file;
        unlink $file           if -l $file;
    }
}

sub create_links {
    my ($links_dir, $home_dir, $only_executable) = @_;
    $only_executable //= 0;

    mkdir $home_dir;
    remove_dead_links($home_dir);

    while (my $file = $links_dir->next) {
        next if $file->stringify eq $links_dir->stringify;
        next if $file->basename =~ /^\.\.$/;
        next if $file->basename =~ /^\.git$/;
        next if $only_executable && ! -x $file;

        my $link = $home_dir->file($file->basename);
        next if -l $link;

        backup($link) || next;

        my $ln = "ln -s '$file' '$link'";
        print $ln, "\n";
        `$ln`;
    }
}

# copy files from $copy_dir to $home_dir
sub copy_files {
    my ($copy_dir, $home_dir) = @_;

    while (my $from = $copy_dir->next) {
        my $basename = $from->basename;
        next if $basename eq $copy_dir->basename;
        next if $basename =~ /^\.\.$/;

        my $to = $home_dir->file($basename);

        backup($to) || next;

        cp($from, $to); # 'cp' preserves file perms. 'copy' doesn't.
    }
}

# Returns 1 if the file was backed up successfully
# Returns 1 if the file does not exist (no need to backup)
# Returns 0 if permisssions don't allow file to be moved
sub backup {
    my $file     = shift;
    my $basename = $file->basename;

    $backup_dir->mkpath;
    my $backup = $backup_dir->file($basename);

    return 1 unless -e $file;

    if (! -o $file) {
        print "Cannot backup $basename to $backup:  Permission denied.\n";
        return 0;
    }

    #print "backing up $basename to $backup\n";
    move($file, $backup);

    return 1;
}

